buildscript {
    ext {
        kotlinVersion = '1.2.41'
        springBootVersion = '2.0.4.RELEASE'
        keycloakVersion = '4.2.0.Final'

        projectVersion = new File("$projectDir/VERSION").text

        if (System.getenv('JENKINS_URL') != null && System.getenv('BUILD_NUMBER') != null) {
            branch = System.getenv('BRANCH_NAME').replaceAll(/[^0-9A-Za-z-]+/, '-')
            branch_suffix = "-" + branch
            if (branch == 'master') {
                branch_suffix = ""
            }
            projectVersion = projectVersion + '.' + System.getenv('BUILD_NUMBER') + branch_suffix
        } else {
            projectVersion = projectVersion + '-SNAPSHOT'
        }
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'nu.peg.web'
version = projectVersion
sourceCompatibility = 1.8

jar {
    baseName = 'pegnu-files'
}

task configurationZip(type: Zip) {
    archiveName = 'pegnu-files-configuration.' + projectVersion + '.zip'
    from(fileTree(dir: 'src/main/resources').include('*.deploy.yml'))
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    maven { url 'https://pkg.pegnu.cloud/repository/m2' }
}

dependencyManagement {
    imports {
        mavenBom "org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}"
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.flywaydb:flyway-core')
    compile('org.springframework.boot:spring-boot-starter-data-solr')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2')
    compile('org.springframework.session:spring-session-core')
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
    compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile('org.keycloak:keycloak-spring-boot-starter')
    compile('org.springframework.boot:spring-boot-configuration-processor')

    runtime('org.springframework.boot:spring-boot-devtools')
    runtime('com.h2database:h2')
    kapt('org.springframework.boot:spring-boot-configuration-processor')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
}
